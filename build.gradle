buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
	}

	repositories {
		mavenCentral()
	}
}
plugins {
	id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.liferay.source.formatter'

repositories {
	jcenter()
}

dependencies {
	testCompile 'junit:junit:4.12'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

def projectVersion = '1.0.2'

formatSource {
	maxLineLength = 120
}

jar {
    manifest {
        attributes("Bundle-Version": projectVersion,
                   "JPM-Command": "gw",
 								   "Main-Class": "com.liferay.gw.GradleWrapper")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task mavenWrapperJar(type: Jar, dependsOn: compileJava) {
	from sourceSets.main.output
	archiveName = "mw"
	manifest {
        attributes(
       		"Bundle-Version": projectVersion,
    			"JPM-Command": "mw",
 			"Main-Class": "com.liferay.gw.MavenWrapper")
	}
}

assemble.dependsOn mavenWrapperJar

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		GwPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId 'com.liferay'
			artifactId 'gw'
			version projectVersion
		}
		MwPublication(MavenPublication) {
			from components.java
			artifact mavenWrapperJar
			groupId 'com.liferay'
			artifactId 'mw'
			version projectVersion
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['GwPublication', 'MwPublication']
	pkg {
		repo = 'maven'
		name = 'gw'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/david-truong/gw.git'
		labels = ['gradle', 'gradle_wrapper']
		version {
			name = projectVersion
			desc = 'Universal Gradle Wrapper command'
			vcsTag = projectVersion
		}
	}
}
