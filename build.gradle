buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
		classpath group: "com.jfrog.bintray.gradle", name: "gradle-bintray-plugin", version: "1.7.3"
	}

	repositories {
		mavenCentral()
		jcenter()
	}
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.liferay.source.formatter'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
	mavenCentral()
}

dependencies {
	testCompile group: "junit", name: "junit", version: "4.12"
}

def projectVersion = '1.0.2'

formatSource {
	maxLineLength = 120
}

jar {
	manifest {
		attributes("Bundle-Version": projectVersion,
			"JPM-Command": "gw",
								   "Main-Class": "com.liferay.gw.GradleWrapper")
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task mavenWrapperJar(type: Jar, dependsOn: compileJava) {
	from sourceSets.main.output
	archiveName = "mw.jar"
	manifest {
		attributes(
			"Bundle-Version": projectVersion,
			"JPM-Command": "mw",
			"Main-Class": "com.liferay.gw.MavenWrapper")
	}
}

assemble.dependsOn mavenWrapperJar

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		GwPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId 'com.liferay'
			artifactId 'gw'
			version projectVersion
		}
		MwPublication(MavenPublication) {
			from components.java
			artifact mavenWrapperJar
			groupId 'com.liferay'
			artifactId 'mw'
			version projectVersion
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['GwPublication', 'MwPublication']
	pkg {
		repo = 'maven'
		name = 'gw'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/david-truong/gw.git'
		labels = ['gradle', 'gradle_wrapper']
		version {
			name = projectVersion
			desc = 'Universal Gradle Wrapper command'
			vcsTag = projectVersion
		}
	}
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"